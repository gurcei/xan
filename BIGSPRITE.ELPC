#output "bigsprite"

#define FULLSONG=1

'============
.declarations
'============
  ' music related vars
  ' ------------------
  #declare music_chunk_index     ' old t
  #declare bar_index             ' old tt
  #declare map_bar_to_chunk(25)  ' old m()
  #declare v1$(15), v2$(15), v3$(15), v4$(15), v5$(15), v6$(15)

  ' palette
  ' -------
  #declare default_red(32)
  #declare default_green(32)
  #declare default_blue(32)
  #declare curr_red(32)
  #declare curr_green(32)
  #declare curr_blue(32)
  #declare fadeval, tempval
  #declare grey

  #declare tempr, tempg, tempb

  #declare vel=1,dist=0

  ' misc
  '-----
  #declare a$ = ""
  #declare idx = 0
  #declare value = 0
  #declare loc
  #declare k
  #declare sprite_frame_idx(8)
  #declare sprite_x(8)
  #declare sprite_y(8)

  #declare state = 0
  #declare counter = 0
  #declare defcolval = 0, defcoldir=1, altcolval = 0, altcoldir=1
  #declare startcol, endcol

  #declare curr_muse = 0

#declare thank_state = 0
#declare thank_idx = 0
#declare s$,sx,sy
#declare thank1$(15)
thank1$(0)="a big thank you to the core mega65 team for all their efforts over the years!"
thank1$(1)="highlander"
thank1$(2)="deft"
thank1$(3)="seriously"
thank1$(4)="adtbm"
thank1$(5)="falk/bluewayws"
thank1$(6)="antti-brain"
thank1$(7)="doubleflash"
thank1$(8)="bit shifter"
thank1$(9)="lgb"
thank1$(10)="sy2002"
thank1$(11)="tayger"
thank1$(12)="lydon"
thank1$(13)="m3wp"
thank1$(14)="feralchild"
thank1$(15)="indiocolifa"

#declare thank2$(16)
thank2$(0)="extra shout outs to:"
thank2$(1)="trenz electronics and the moulds team!"
thank2$(2)="deathy for all your passion and dedication to the mega65!"
thank2$(3)="bit shifter for all the great basic 65 enhancements!"
thank2$(4)="lydon for all your efforts preparing batch#2!"
thank2$(5)="sy2002, mjoergen and lydon for all the additional cores!"
thank2$(6)="dddaaannn for all your documentation, tooling and community-building!"
thank2$(7)="tayger for mega65connect and the 'files.mega65.org' filehost!"
thank2$(8)="impakt for the determined and thorough manual proof-reading!"
thank2$(9)="thechief for your efforts supporting the nexys board!"
thank2$(10)="hojo for the awesome new front covers you made for all our manuals!"
thank2$(11)="amok for all your documentation efforts!"
thank2$(12)="ubik for the awesome eleven ide!"
thank2$(13)="maurice for testing every single basic 65 example in the manual!"
thank2$(14)="retrocombs, perifractic and rb-jeffrey for your awesome youtube vids!"
thank2$(15)="mindrail for the macguyver-jtag video!"
thank2$(16)="deft and adtbm for all your 'hello mega65 community' videos!"

#declare thank3$(60)
thank3$(0)="thanks to all the 1st gen developers that made demos, utilities and games!"
thank3$(1)="adtbm"
thank3$(2)="airjuri"
thank3$(3)="akmafin"
thank3$(4)="amok"
thank3$(5)="dddaaannn"
thank3$(6)="deathy"
thank3$(7)="deft"
thank3$(8)="dextrous"
thank3$(9)="dillof"
thank3$(10)="dirk"
thank3$(11)="docster"
thank3$(12)="egonolsen71"
thank3$(13)="elch"
thank3$(14)="endurion"
thank3$(15)="feralchild"
thank3$(16)="fredrikr"
thank3$(17)="geehaf"
thank3$(18)="geirs"
thank3$(19)="grim-fandango"
thank3$(20)="grubi"
thank3$(21)="heath-man"
thank3$(22)="hernandp"
thank3$(23)="hstampfl"
thank3$(24)="james"
thank3$(25)="jespergravgaard"
thank3$(26)="johan"
thank3$(27)="johnwayner"
thank3$(28)="kidra"
thank3$(29)="kithkanan"
thank3$(30)="kryan1"
thank3$(31)="lak132"
thank3$(32)="liquiddream"
thank3$(33)="lydon"
thank3$(34)="m3wp"
thank3$(35)="majikeyric"
thank3$(36)="maurice"
thank3$(37)="maxice"
thank3$(38)="mc64"
thank3$(39)="mightyaxle"
thank3$(40)="mr.toast"
thank3$(41)="mteufel"
thank3$(42)="n1k0m0"
thank3$(43)="neoman"
thank3$(44)="oziphantom"
thank3$(45)="retrocombs"
thank3$(46)="shallan50k"
thank3$(47)="snoopy"
thank3$(48)="stepz"
thank3$(49)="stigodump"
thank3$(50)="sy2002"
thank3$(51)="tayger"
thank3$(52)="thechief"
thank3$(53)="ubik"
thank3$(54)="urbanl"
thank3$(55)="waulok"
thank3$(56)="wombat"
thank3$(57)="zeha"
thank3$(58)="zzsila"
thank3$(59)=""
thank3$(60)=""

#declare muse_state = 0
#declare next_spr = 1
#declare next_frm = 0
#declare next_cnt = 0
#declare spr_active(8)
#declare spr_angle = 0
#declare greys(3)
greys(0)=15:greys(1)=12:greys(2)=11

#declare muse_clr(9)
muse_clr(0)=16
muse_clr(1)=18
muse_clr(2)=20
muse_clr(3)=22
muse_clr(4)=25
muse_clr(5)=26
muse_clr(6)=28
muse_clr(7)=29
muse_clr(8)=31

#declare thank_start_idx

  #declare SPRX64EN = $d057
  #declare SPRHGTEN = $d055
  #declare SPRHGT   = $d056

  #declare SCRNPTRLSB = $d060
  #declare SCRNPTRBNK = $d062

  #declare SPRPTR16 = $d06e
  #declare SPRPTRADR = $d06c
  #declare SPRPTRBNK = $d06e

  #declare HOTREG_7 = $d05d

'----
.main
'----
  gosub init_default_colours
  gosub init_music
  gosub p1_drop_papers
  'gosub p2_colour_cycle_static_muses
  'gosub p3_animate_muse1_2_3_4
  'gosub p4_pulse_background_mural_to_chorus
  'gosub p5_animate_muse_5_6_7_8_9
  'gosub p6_pulse_background_mural_2nd_time
  gosub p7_big_olivia_with_sprites
  'gosub p8_muses_return_to_petscii_mural
  'gosub p9_pulse_background_mural_3rd_time
  'gosub p10_petscii_dancers_come_to_life
  'gosub p11_expanded_or_tiled_sprites
  'gosub p12_muses_dancing_in_circle
  'gosub p13_hollywood_dash
  'gosub p14_finale

.init_default_colours
'--------------------
#declare debug_flag = 0
  if peek($d020)<>0 and peek($d020)<>2 then begin
    border 0
    poke $d018, peek($d018) and $fd
    edma 0, $300, $ffd3100, $48000
    edma 3, $300, 0, $ffd3100
    ^^bload "mural.bin",p($800),r
    ^^bload "mural.clr",p($1f800),r
    debug_flag = 1
  bend

  for k = 0 to 31
    default_red(k) = peek($48000+k)
    default_green(k) = peek($48100+k)
    default_blue(k) = peek($48200+k)
  next k

  if debug_flag=1 then begin
    ^^bload "mural.bin",p($48000),r
    ^^bload "mural.clr",p($49000),r
  bend

  return

.fade_colours_to_grey
'--------------------
'INPUT: fadeval = 0 to 16
  tempval = (16-fadeval) / 16
  for k = 0 to 31
    grey = (default_red(k) + default_green(k) + default_blue(k)) / 3
    poke $d100+k, int(grey + (curr_red(k) - grey) * tempval)
    poke $d200+k, int(grey + (curr_green(k) - grey) * tempval)
    poke $d300+k, int(grey + (curr_blue(k) - grey) * tempval)
  next k
  return

.fade_colours_to_defaults
'------------------------
  'INPUT: fadeval=0 to 16
  tempval = fadeval / 16
  for k = startcol to endcol
    poke $d100+k, curr_red(k) + (default_red(k)-curr_red(k)) * tempval
    poke $d200+k, curr_green(k) + (default_green(k)-curr_green(k)) * tempval
    poke $d300+k, curr_blue(k) + (default_blue(k)-curr_blue(k)) * tempval
  next k
  return

.fade_colours_to_black
'---------------------
  'INPUT: fadeval=0 to 16
  tempval = (16-fadeval) / 16
  for k = startcol to endcol
    poke $d100+k, curr_red(k) * tempval
    poke $d200+k, curr_green(k) * tempval
    poke $d300+k, curr_blue(k) * tempval
  next k
  return

'==============
.p1_drop_papers
'==============
  gosub p1_init
  gosub p1_loop
  gosub p1_ender
  return


.p1_init
'-------
  ' load 13 standard sprites for paper
  ^^bload "paper.dat",p($47c40),r

  ' load 64x64 sprite data
  ^^bload "muse.dat",p($40040),r

  ' load oli frames to bank 5
  ^^bload "oli.dat",p($50000),r

  ' disable any 64-bit wide sprites
  poke SPRX64EN, 0

  ' all sprites normal height
  poke SPRHGTEN, 0

  ' enforce sprites into BANK4 and SPRPTR16 bit turned on
  poke SPRPTRBNK, 4+128
  wpoke SPRPTRADR, 0

  ' prepare all sprite
  for k = 0 to 7
    sprite_frame_idx(k) = int(rnd(1)*13)
    sprite_x(k) = 30+40*k
    sprite_y(k) = int(rnd(1)*5)*5
    sprite k, 1, 15
    movspr k, 0#0
    movspr k, sprite_x(k), sprite_y(k)
    wpoke ($40000+k*2), $11f1 + sprite_frame_idx(k)
  next k

  fadeval = 0
  return

.store_curr_colours
'------------------
  for k = 0 to 31
    curr_red(k)=peek($d100+k)
    curr_green(k)=peek($d200+k)
    curr_blue(k)=peek($d300+k)
  next k
  return

.p1_loop
'-------
  ' drop paper sprites down
  if state < 4 and muse_state=0 then begin
    for k = 0 to 7
      if int(rnd(1)*3) = 0 then begin
        sprite_frame_idx(k) = mod(sprite_frame_idx(k)+1,13)
      bend
      if sprite_y(k) < 247 then begin
        sprite_y(k) = sprite_y(k) + int(rnd(1)*3)
        sprite_x(k) = sprite_x(k) + int(rnd(1)*5)-2
        movspr k, sprite_x(k), sprite_y(k)
        wpoke ($40000+k*2), $11f1 + sprite_frame_idx(k)
      bend
    next k
  bend

  gosub poll_play
  sleep .025

  ' fade in to grey
  ' ---------------
  if state = 0 then begin
    if mod(counter,5)=0 then begin
      gosub fade_colours_to_grey
      fadeval = fadeval + 1
      if fadeval > 16 then state = state + 1:fadeval = 0:gosub store_curr_colours:counter=0
    bend
    counter = counter + 1
  bend

  ' ebb-fade to colours for default palette
  ' ---------------------------------------
  if state = 1 then begin
    fadeval = defcolval
    startcol = 0:endcol = 15
    if counter > 3 then endcol = 31
    gosub fade_colours_to_defaults
    defcolval = defcolval + defcoldir
    if defcolval = 16 then defcoldir=-1
    if defcolval = 0 then defcoldir=1:counter=counter+1
    if counter = 6 and defcolval = 15 and defcoldir=-1 then state=state+1:counter=0
  bend

  ' colour-cycle muses
  ' ------------------
  if state = 2 then begin
    dist = dist + vel
    if dist >= 1 then begin
      dist = dist - 1
      counter = counter + 1
      startcol = 16:endcol = 31
      gosub cycle_colours
      if counter > 16*2 then vel=vel*.93
      if counter = 16*3-1 then state = 3:palette restore:counter=0:defcolval=0:defcoldir=2
    bend
  bend

  if state = 3 then gosub highlight_curr_muse

  if state = 4 then gosub fade_out

  if state = 5 then gosub fade_out

  if state = 6 then gosub focus_muse

  get a$:if a$="" then goto p1_loop

  return

.focus_muse
'----------
  if muse_state = 0 then begin
    gosub init_muse_sprites
    muse_state=1
    counter = 0
    print "{x93}";
    return
  bend
  if muse_state = 1 then begin
    fadeval=counter
    gosub fade_colours_to_defaults
    if counter=16 then counter=0:muse_state=2
  bend

  if muse_state = 2 then gosub show_thanks

  if muse_state = 3 then begin
    fadeval=counter
    'print "{x13}";chr$(27);"q";fadeval;:getkey s$
    startcol=0:endcol=31:gosub fade_colours_to_black
    if counter = 16 then counter=0:muse_state = 4
  bend

  if muse_state = 4 then begin
    'print "{x13}";chr$(27);"q muse_state=4";fadeval;:getkey s$
    for k = 0 to 7:sprite k,0:next k
    bank 128
    gosub restore_screen
    curr_muse = curr_muse + 1
    muse_state = 5
    counter = 0
  bend

  if muse_state = 5 then begin
    fadeval=counter
    startcol=0:endcol=31:gosub fade_colours_to_defaults
    if counter=16 then begin
      muse_state = 6
      counter = 0
      state = 2:dist = 0:vel = 1
    bend
  bend

  if muse_state > 0 and muse_state < 4 and state=6 then begin
    gosub anim_muse_sprites
  bend
  
  counter = counter + 1

  return

.draw_clr_text
'-------------
  cursor sx+counter,sy:color mod(counter,16)+16:print mid$(s$,1+counter,1);
  if counter = len(s$) then counter = -1
  return

.show_thanks
'-----------
  if thank_state = 0 and counter > 5 then thank_state = 1:counter=0
  if thank_state = 1 then begin
    s$=thank1$(0):sx=1:sy=0:gosub draw_clr_text
    if counter <> -1 then counter=counter+1:gosub draw_clr_text
    if counter = -1 then thank_state = 2:thank_idx=1
  bend
  ' pause
  if thank_state = 2 and counter > 5 then begin
    thank_state = 3:counter = 0
  bend
  ' draw next name
  if thank_state = 3 then begin
    if thank_idx < 16 then begin
      s$=thank1$(thank_idx)
      if mod(thank_idx,2) = 1 then sx=10-len(s$)/2:else sx=70-len(s$)/2
      sy = int((thank_idx-1) / 2)*3 + 3
      gosub draw_clr_text
      if counter <> -1 then counter=counter+1:gosub draw_clr_text
    bend
    if counter = -1 or thank_idx=16 then begin
      thank_idx = thank_idx + 1
      if thank_idx > 16 then begin
        thank_state = 4
        muse_state=3
        counter=0
        palette restore
        gosub store_curr_colours
      bend:else thank_state = 2
    bend
  bend

  ' 2nd page of thanks
  if curr_muse >= 1 and curr_muse <=4 then gosub thanks_2

  ' 3rd page of thanks
  if curr_muse >=5 and curr_muse <=6 then gosub thanks_3

  if thank_state<>4 and thank_state<>8 then begin
    startcol=16:endcol=31:gosub cycle_colours
  bend

  return

.thanks_2
'--------
  if thank_state = 4 and counter > 10 then thank_state = 5:counter=0
  if thank_state = 5 then begin
    s$=thank2$(0):sx=40-len(s$)/2:sy=0:gosub draw_clr_text
    if counter > -1 then counter=counter+1:gosub draw_clr_text
    if counter = -1 then thank_state = 6:thank_start_idx=1+(curr_muse-1)*4:thank_idx=thank_start_idx
  bend
  ' pause
  if thank_state = 6 and counter > 5 then begin
    thank_state = 7:counter = 0
  bend

#define MAX_SHOUTS_PER_PAGE = 4
  ' draw next shout out
  if thank_state = 7 then begin
    if thank_idx-thank_start_idx < MAX_SHOUTS_PER_PAGE then begin
      s$=thank2$(thank_idx)
      sx=40-len(s$)/2
      sy=(thank_idx - thank_start_idx) * 3 + 3
      if sy > 6 then sy = sy + 11
      gosub draw_clr_text
      if counter > -1 then counter=counter+1:gosub draw_clr_text
    bend
    if counter = -1 or thank_idx-thank_start_idx=MAX_SHOUTS_PER_PAGE then begin
      thank_idx = thank_idx + 1
      if thank_idx - thank_start_idx >= MAX_SHOUTS_PER_PAGE+1 then begin
        thank_state = 4
        if curr_muse = 4 then thank_state = 8
        muse_state=3
        counter=0
        palette restore
        gosub store_curr_colours
      bend:else thank_state = 6
    bend
  bend

  return

.thanks_3
'--------
  if thank_state = 8 and counter > 10 then thank_state = 9:counter=0
  if thank_state = 9 then begin
    s$=thank3$(0):sx=40-len(s$)/2:sy=0:gosub draw_clr_text
    if counter = -1 then thank_state = 10:thank_start_idx=1+(curr_muse-5)*30:thank_idx=thank_start_idx
  bend
  ' pause
  if thank_state = 10 and counter > 5 then begin
    thank_state = 11:counter = 0
    if thank_idx-thank_start_idx >= 30 then begin
      thank_state = 12
      if curr_muse = 5 then thank_state = 8
      muse_state = 3
      counter = 0
      palette restore
      gosub store_curr_colours
      return
    bend
  bend

  ' draw next name
  if thank_state = 11 then begin
    if thank_idx-thank_start_idx < 30 then begin
      s$=thank3$(thank_idx)
      for k = 1 to len(s$)
        if mod(thank_idx,2) = 1 then sx=10-len(s$)/2:else sx=70-len(s$)/2
        sy = int((thank_idx-thank_start_idx) / 2) + 5
        gosub draw_clr_text
        counter = counter + 1
      next k
    bend
    thank_idx = thank_idx + 1
    thank_state = 10:counter = 0
  bend

  return

.anim_muse_sprites
'-----------------
  next_cnt = next_cnt + 1
  if next_cnt = 5 then begin
    next_cnt = 0
    bank 4
      wpoke next_spr*2,$1001+8*(next_frm + 6 * curr_muse)
      wpoke 0,wpeek(next_spr*2)
    bank 128
    movspr next_spr, 150, 120
    movspr 0, 150, 120
    movspr next_spr, spr_angle#1
    spr_angle=mod(spr_angle+10,360)
    sprite next_spr, 1, greys(0)
    sprite 0, 1, 1
    spr_active(next_spr) = 1
    next_spr = mod(next_spr+5, 7)+1
    next_frm = mod(next_frm+1, 6)
  bend

  for k = 1 to 7
    if spr_active(k)>0 then begin
      spr_active(k)=spr_active(k)+1
      fadeval=spr_active(k)/10
      if fadeval < 3 then begin
        sprite k, 1, greys(fadeval)
      bend:else sprite k, 0:spr_active(k)=0
    bend
  next k
  return

.init_muse_sprites
'-----------------
  ' turn of all sprites
  for k = 0 to 7
    sprite k, 0
    movspr k, 160, 80
  next k
  poke SPRX64EN, 255
  poke SPRHGTEN, 255
  poke SPRHGT, 64
  poke SPRPTRBNK, 4+128
  wpoke SPRPTRADR, 0
  return


.save_screen
  ^^edma 0, $7d0, $800, $48000
  ^^edma 0, $7d0, $1f800, $49000
  return

.restore_screen
  ^^edma 0, $7d0, $48000, $800
  ^^edma 0, $7d0, $49000, $1f800
  return

.fade_out
'--------
  fadeval = counter
  gosub fade_colours_to_black
  counter = counter + 1
  if counter > 16 then state = state + 1:startcol=0:endcol=31:counter=0:gosub store_curr_colours:muse_state=0
  return

.highlight_curr_muse
'-------------------
    fadeval = defcolval
    k = muse_clr(curr_muse)
    startcol = k:endcol = k
    gosub fade_colours_to_white
    defcolval = defcolval + defcoldir
    if defcolval = 16 then defcoldir=-2
    if defcolval = 0 then defcoldir=2:counter=counter+1
    if counter = 3 and defcolval = 14 and defcoldir=-2 then begin
      state=state+1
      counter=0
      palette restore
      fadeval=0
      gosub save_screen
    bend
    return

.fade_colours_to_white
'---------------------
  'INPUT: fadeval=0 to 16
  tempval = fadeval / 16
  for k = startcol to endcol
    poke $d100+k, default_red(k) + (15 - default_red(k)) * tempval
    poke $d200+k, default_green(k) + (15 - default_green(k)) * tempval
    poke $d300+k, default_blue(k) + (15 - default_blue(k)) * tempval
  next k
  return
  

.cycle_colours
'-------------
  tempr = peek($d100+startcol)
  tempg = peek($d200+startcol)
  tempb = peek($d300+startcol)

  for k = startcol to endcol
    poke $d100+k, peek($d100+k+1)
    poke $d200+k, peek($d200+k+1)
    poke $d300+k, peek($d300+k+1)
  next k
  poke $d100+endcol, tempr
  poke $d200+endcol, tempg
  poke $d300+endcol, tempb

  return


.p1_ender
'--------
  return


'==========================
.p7_big_olivia_with_sprites
'==========================
  gosub p7_init
  gosub p7_loop
  gosub p7_ender
  return

'----
.p7_init
'----
  ' switch to 80x50
  bank 128
  setbit HOTREG_7, 7
  poke $d031, $e8 ' 1110 1000 = H640,FAST,ATTR,V400

  ' move SCRNPTR to bank4 - $8000
  wpoke SCRNPTRLSB, $8000
  poke SCRNPTRBNK,$04

  ' let all sprites be 64 bits wide
  poke SPRX64EN, 255

  ' let all sprites be 64 bits high
  poke SPRHGTEN, 255
  poke SPRHGT, 64

  ' $4,0000 +------------------+
  '         ! SPRPTR (8 words) !
  ' $4,0040 +------------------+ <-- sprptr $1001
  '         ! MUSE.DAT         !
  '         ! ========         !
  '         ! MUSE#1 - frame 0 !
  ' $4,0240 +..................+ <-- sprptr $1001 + 8
  '         ! MUSE#1 frames1-5 !
  ' $4,0E40 +..................+ <-- sprptr $1001 + 8*6
  '         ! MUSE#2 frames0-5 !
  ' $4,1A40 +..................+ <-- sprptr $1001 + 8*12
  '         ! MUSES#3 to #8    !
  ' $4,6040 +..................+
  '         ! 32 x RGB PALETTE !
  ' $4,6CA0 +..................+
  '         ! oli - frame 0    !
  ' $4,7C40 +------------------+
  '         ! PAPER.DAT        !
  '         ! =========        !
  '         ! PAPER - frame 0  !
  ' $4,7C80 +..................+
  '         ! PAPER - frame 1  !
  ' $4,7CC0 +..................+
  '         ! PAPER frm 2-12   !
  ' $4,7F80 +------------------+

  ' $4,8000 +------------------+
  '         ! SCREEN MEM 80x50 !
  ' $4,8FA0 +------------------+

  ' $5,0000 +------------------+
  '         ! OLI.DAT          !
  ' $5,FA00 +------------------+

  ' enforce sprites into BANK4 and SPRPTR16 bit turned on
  poke SPRPTRBNK, 4+128           
  wpoke SPRPTRADR, 0

  ' switch palette
  #declare red,green,blue
  loc=$46040
  for idx=0 to 31
    red=peek(loc):loc=loc+1
    green=peek(loc):loc=loc+1
    blue=peek(loc):loc=loc+1
    ' print "r=";red;", g=";green;", b=";blue
    poke $d100+idx,red
    poke $d200+idx,green
    poke $d300+idx,blue
  next idx

  ' clear 80x50 chars and colour ram
  for idx=0 to 3999:poke $48000+idx,160:poke $ff80000+idx,6:next idx
      
  ' display sprite
  #declare sprclr,offx
  sprclr = 30
  offx=25
  sprite 0,1,sprclr
  movspr 0,0+offx,50
  sprite 1,1,sprclr
  movspr 1,85+offx,50
  sprite 2,1,sprclr
  movspr 2,171+offx,50
  sprite 3,1,sprclr
  movspr 3,256+offx,50

  sprite 4,1,sprclr
  movspr 4,0+offx+8,180
  sprite 5,1,sprclr
  movspr 5,85+offx+8,180
  sprite 6,1,sprclr
  movspr 6,171+offx,180
  sprite 7,1,sprclr
  movspr 7,256+offx,180

  ' point sprite0 to $4040
  bank 4
  for k=0 to 14 step 2
    wpoke k, $1001+8*(3*k)
  next k

  ' set background, border
  background 6
  border 14

  idx = 0
  ^^oi=0:od=0
  return


/----
.p7_loop
'----
  ' select sprite frame
  bank 4
  for k=0 to 7
    wpoke k*2, $1001+8*(idx+6*k)
  next k

  gosub drawolivia

  gosub poll_play
  sleep .3
  idx=idx+1
  if idx=6 then idx=0
  get a$:if a$="" then goto p7_loop
  return


'-----
.p7_ender
'-----
  for idx = 0 to 7
    sprite idx,0
  next idx

  ' switch to 80x25
  bank 128
  poke $d031, $e0 ' 1110 0000 = H640,FAST,ATTR

  ' restore SCNPTR to bank 0 - $0800
  wpoke SCRNPTRLSB, $0800
  poke SCRNPTRBNK, 0

  palette restore
  end


'----------
.drawolivia
'----------
  #declare x,y,z,bval,olidx
  ^^if oi=0 then lo=$460a0
  ^^if oi>0 then lo=$50000+4000*(oi-1)

  loc=$46ca0 ' first olivia frame
  ^^edma 0,4000,lo,$ff80000

  ^^if od = 0 then oi = oi + 1:if oi > 8 then od = 1:oi=8
  ^^if od = 1 then oi = oi - 1:if oi < 1 then od = 0

  return

'==========
.init_music
'==========
  play
  tempo 17
  bar_index=0
  map_bar_to_chunk(0)=0
  map_bar_to_chunk(1)=0
  map_bar_to_chunk(2)=1
  map_bar_to_chunk(3)=2
  map_bar_to_chunk(4)=2
  map_bar_to_chunk(5)=3
  map_bar_to_chunk(6)=4
  map_bar_to_chunk(7)=2
  map_bar_to_chunk(8)=2
  map_bar_to_chunk(9)=3
  map_bar_to_chunk(10)=4
  map_bar_to_chunk(11)=5
  map_bar_to_chunk(12)=6
  map_bar_to_chunk(13)=7
  map_bar_to_chunk(14)=8
  map_bar_to_chunk(15)=9
  map_bar_to_chunk(16)=9
  map_bar_to_chunk(17)=10
  map_bar_to_chunk(18)=11
  map_bar_to_chunk(19)=12
  map_bar_to_chunk(20)=13
  map_bar_to_chunk(21)=14
  map_bar_to_chunk(22)=15

  envelope 8,0,12,3,7
  envelope 1,0,9,9,12,2,2048

  ' *** intro wailing ***
  ' ---------------------
  v1$(0)="o5t4m9wfffhf"
  v2$(0)="o3t5m0sp0cp1o5rcrcrcrcrcrcrcr p0o5crp1o3crcrcrcrcrcrcr p0o4gr p1o2grgrgrgrgrgrgr p0o4er p1o2ererer"
  v3$(0)="m1p0t6o1ife#defd#defe#defe#defe#defe#defe#de"
  v4$(0)="o5t4m9w#f#f#fh#f"
  v5$(0)="o3t5m0sp0#cp1o5r#cr#cr#cr#cr#cr#cr#cr p0o5#crp1o3#cr#cr#cr#cr#cr#cr#cr p0o4#gr p1o2#gr#gr#gr#gr#gr#gr#gr p0o4#er p1o2#er#er#er"
  v6$(0)="m1t9o6segeg#d#f#d#fdfdf#ce#ce fafae#ge#g#dg#dgd#fd#f gbgb#f#a#f#afafae#ge#g fa#f#afa#f#a"

  ' *** first riff ***
  ' ------------------
  v1$(1)="t7p0m1o2qfo3c o2igggggggggggg o2qfo3c o2igggggggggggg"
  v2$(1)="t9 p0o6 qfe sgr p2o2grgrgrgrgrgrgrgrgrgrgr":v2$(1)=v2$(1)+v2$(1)
  v3$(1)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr"
  v4$(1)="t7p0m1qo3cg idddddddddddd qo3cg idddddddddddd"
  v5$(1)="t9 p0o5qag sbr p1o1brbrbrbrbrbrbrbrbrbrbr":v5$(1)=v5$(1)+v5$(1)
  v6$(1)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr"

  ' rem *** i'm alive! ***
  ' ---------------------
  v1$(2)="t7m5o5qp0gg grirp7 e#f qgigqg#fe he sede#fe#f hdir"
  v2$(2)="t7p0m1o2qfo3c o2qggigqgio3c o2qeeieqeia qccicqcie qdd"
  v3$(2)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr"
  v4$(2)="t7m5o6qp0fp9sedef qdrir t0o6p0ep8#f qgigqg#fe he sede#fe#f hdir"
  v5$(2)="t7p0m1o3qcg ddidqdig o2qbbibqbo3ie o2 qggigqgib qaa"
  v6$(2)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr"

#ifdef FULLSONG
  ' *** seems like forever ***
  v1$(3)="t7m5o5qp0idp5scdicso4bo5c qo4bio5cqc t2p0iep5g#f qrr t7m5o5p0idp5scdicso4bo5c qo4bio5cqc t2p0iep5g#f q.r p0t7i#d p7s#dd#de#fe#fghgr"
  v2$(3)="t7p0m1o3qdo2#f qaa io3eceqdiddd o3qdo2#f qaa io3eceqdiddd ee#f#f q.eieeeee"
  v3$(3)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"
  v4$(3)="t0m4o4p0idp8scdicso3bo4c qo3bio4cqc o5t2p0igp5ba qrr t0m4o4p0idp8scdicso3bo4c qo3bio4cqc o5t2p0igp5ba q.r o4p0t0i#dp9s#dd#de#fe#fghgr"
  v5$(3)="t7p0m1o3qad ee igeg q#f i#f#f#f qad ee igeg q#f i#f#f#f ggaa q.b ibbbbb"
  v6$(3)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"

  ' *** im alive (chorus1) ***
  ' --------------------------
  v1$(4)="t7m5o5p0qaa p3h.bp1qaq.air qp0aa p3h.bp1qaq.air"
  v2$(4)="t7p0m1o3qddigggggggg dddddddd gggggggg dddd"
  v3$(4)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"
  v4$(4)="t7m5o6p0qddwdqdr ddwdqdr"
  v5$(4)="t7p0m1o3qaa o4idddddddd o3iaaaaaaaa o4dddddddd o3iaaaa"
  v6$(4)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"

  ' *** i'm aliveclose(chorus2 ending) ***
  ' --------------------------------------
  v1$(5)="t7m5o5p0qaa hbiab o6co5hbirsap9gababga w.g"
  v2$(5)="t7p0m1o3qdd gg#f#feeddw.c"
  v3$(5)="t3o2 ibrbr o1gr gr o2 gr o1 gr gr gr o2 gr o1 gr t0o2p0s#cr o5p1 #cr#cr#cr#cr#cr#cr#cr#cr#cr#cr#cr"
  v4$(5)="t7m5o5p0q#f#f hgi#fgahgirs#fp9e#fg#fge#f w.c"
  v5$(5)="t7p0m1o3qaa o4ddcco3bbaaw.g"
  v6$(5)="t3o2 ibrbr o1gr gr o2 gr o1 gr gr gr o2 gr o1 gr t0o2p0scr o5p1 crcrcrcrcrcrcrcrcrcrcr"

  ' *** instrumental ***
  ' --------------------
  v1$(6)="t7m5p5o5 qrr r q.g ied o4qb io5deg p0sdo4bo5dedo4bip9g o4ieggeqgie qbo5iddo4bo5qde p9o5r q.giedqesgegeigbo6do5sp0baig"
  v1$(6)=v1$(6)+"o5qp9ibp0sgbo6ido5sbo6d sp0g#fdo5b o6#fdo5bg p9ibo6cdo5ib"
  v2$(6)="t7p0m1o2qfo3c o2qggigqgio3c o2qeeieqeia o1qaaiaqaio2e qdd  fo3c o2qggigqgio3c o2qeeieqeia o1qaaiaqaio2e qdd"
  v3$(6)="p0t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr":v3$(6)=v3$(6)+v3$(6)
  v4$(6)="qrrt2p0so6b#f#afae#g#dgd#f#cfceo5bo6#do5#ao6do5ao6#co5#go6co5gb#f#afae#gdo3scrp2t7o6crcrcrcrcrcrcrcrcrcrcrcrcrgrgr"
  v4$(6)=v4$(6)+"t6o6p0sdp6o5co3p9co2co5p0sbp6o5co3p9co2co5p0sgp6o5co3p9o1cco5p0sgp6o5co3p9o1cc"
  v4$(6)=v4$(6)+"o4p0grp1o2crcrcrcrcrcrcrt0o5p0segegf#gf#g#fa#fagbgbt7o6sgp3o2wc"
  v5$(6)="t7p0m1o3qcg ddidqdig o2qbbibqbo3ie qeeieqeig q#f#f  cg ddidqdig o2qbbibqbo3ie qeeieqeig q#f#f"
  v6$(6)="p0t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr":v6$(6)=v6$(6)+v6$(6)

  ' *** suddenly came the dawn ***
  ' ------------------------------
  v1$(7)="t7m5o5qp0idp5scdicso4bo5c qo4bio5cqc t2p0iep5g#f qrr t7m5o5p0idp5scdicso4bo5c qo4bio5cqc t2p0iep5g#f q.r p0t7i#d p7s#dd#de#fe#fghg"
  v2$(7)="t7p0m1o3qdo2#f qaa io3eceqdiddd o3qdo2#f qaa io3eceqdiddd ee#f#f q.eieeeee"
  v3$(7)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"
  v4$(7)="t0m4o4p0idp8scdicso3bo4c qo3bio4cqc o5t2p0igp5ba qrr t0m4o4p0idp8scdicso3bo4c qo3bio4cqc o5t2p0igp5ba q.r o4p0t0i#dp9s#dd#de#fe#fghgr"
  v5$(7)="t7p0m1o3qad ee igeg q#f i#f#f#f qad ee igeg q#f i#f#f#f ggaa q.b ibbbbb"
  v6$(7)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"

  ' *** i'm alive! chorus2 ***
  ' -------------------------
  v1$(8)="t7m5o5p0qaa p3hb qbbo6h.#c o51qbq.bir qp0bb p3h.o6#cp1o5qbq.bir"
  v2$(8)="t7p0m1o3qddhg qee sarararararararar erererererererer arararararararar erererer"
  v3$(8)="t3o2 ibrbr brrr brbr o1s grgrgrgrgrgrgrgrgrgrgrgrgrgrgrgr grgrgrgrgrgrgrgrgrgrgrgr"
  v4$(8)="t7m5o6p0qdd hd qee ro5ep9o6eid#co5hb p0o6qee ro5ep9o6eid#co5hb"
  v5$(8)="t7p0m1o3qaao4hd o3qbb o4serererererererer o3brbrbrbrbrbrbrbr o4erererererererer o3brbrbrbr"
  v6$(8)="t3o2 ibrbr brrr brbr o1s grgrgrgrgrgrgrgrgrgrgrgrgrgrgrgr grgrgrgrgrgrgrgrgrgrgrgr"

  ' *** im alive! verse pitch shift ***
  ' -----------------------------------
  v1$(9)="t7m5o5qp0aa arirp7 #f#g qaiaqa#g#f h#f s#fe#f#g#f#g heir"
  v2$(9)="t7p0m1o2qgo3d o2qaaiaqaio3d o2q#f#fi#fq#fib qddidqdi#f qee"
  v3$(9)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr"
  v4$(9)="t7m5o6qp0gp9s#fd#fg qerir t0o6p0#fp8#g qaiaqa#g#f h#f s#fe#f#g#f#g heir"
  v5$(9)="t7p0m1o3qda eeieqeia o3q#c#ci#cq#ci#f o2 qaaiaqaio3#c o2qbb"
  v6$(9)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr"

  ' *** seems like forever ***
  v1$(10)="t7m5o5qp0iep5sdeids#co5d q#cio5dqd t2p0i#fp5a#g qrr t7m5o5p0iep5sdeids#cd q#cio5dqd t2p0i#fp5a#g q.r p0t7if p7sfef#f#g#f#gahar"
  v2$(10)="t7p0m1o3qeo2#g qbb io3#fd#fqeieee o3qeo2#g qbb io3#fd#fqeieee #f#f#g#g q.#fi#f#f#f#f#f"
  v3$(10)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"
  v4$(10)="t0m4o4p0iep8sdeids#cd q#cio4dqd o5t2p0iap5o6#co5b qrr t0m4o4p0iep8sdeids#cd q#cio4dqd o5t2p0iap5o6#co5b q.r o4p0t0ifp9sfef#f#g#f#gahar"
  v5$(10)="t7p0m1o3qbe #f#f ia#fa q#g i#g#g#g qbe #f#f ia#fa q#g i#g#g#g aabb qo4.#c i#c#c#c#c#c"
  v6$(10)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gr o2gr o1gg o2gr o1gg o2gr"

  ' *** im alive chorus3 ***
  v1$(11)="t7m5o5p0qbb p3ho6#c o5ibo6#cdh#cir o5qbb p3ho6#c o5ibo6#cdh#cir o5qbb ho6#c o5ibo6#cdh#cir sp9o5babo6#co5bo6#co5ab"
  v2$(11)="t7p0m1o3qee saeaeaeae #ge#ge#ge#ge #fe#fe#fe#fe ebebebeb saeaeaeae#ge#ge#ge#ge#fe#fe#fe#fe ebebebeb saeaeaeae#ge#ge#ge#ge#fe#fe#fe#fe ebebebeb"
  v3$(11)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr"
  v4$(11)="t7m5o5p0q#g#g p3ha i#gabhair q#g#gha i#gabhair q#g#gha i#gabhair p9s#g#f#ga#f#ge#f"
  v5$(11)="qt6o4ee o5ip0ae#co4a o5be#co4a o6#co5e#co4a o6eo5b#ge o5ip0ae#co4a o5be#co4a o6#co5e#co4a o6eo5b#ge"
  v5$(11)=v5$(11)+"o5sp0ae#ceae#ce be#cebe#ce o6#co5e#ceo6#co5e#ce o6eo5b#gbo6eo5b#gb"
  v6$(11)="t3o2 ibrbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr brbr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr"

  ' *** final interlude intro ***
  v1$(12)="t8m5o5p0 h.#firs#f#f q.gsggigsrigsrig":v1$(12)=v1$(12)+v1$(12)+v1$(12):v1$(12)=v1$(12)+"h.#firs#f#f q.gsggifsrifsrif"
  v2$(12)="t8o3h.dirsdd q.dsddo2iasriasria o3h.dirsdd q.dsddidsridsrid h.dirsdd q.dsddo2iasriasria h.dirsdd q.dsdd o2i#gsri#gsri#g"
  v3$(12)="t3o2 qbrrirsbb q.bsbbidsridsrid qbrrirsbb q.bsbbidsridsrid o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr idsridsrid"
  v4$(12)="m0p0t7o1h.dirsdd q.dsddidsridsrid":v4$(12)=v4$(12)+v4$(12)+v4$(12):v4$(12)=v4$(12)+"h.dirsdd q.dsdd i#gsri#gsri#g"
  v5$(12)="t8o3h.airsaa q.asaaidsridsrid h.airsaa q.asaaiasriasria h.airsaa q.asaaidsridsrid h.airsaa q.asaa i#csri#csri#c"
  v6$(12)="t3o2 qbrrirsbb q.bsbbidsridsrid qbrrirsbb q.bsbbidsridsrid o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr idsridsrid"

  ' *** final interlude 1 ***
  v1$(13)="o5t8w.e qo6idp5sri#csr o5ib h.a irb wb we o6h#c o5a wg r"
  v2$(13)="t6o2 iaaaaaaaa aaaaaaaa eeeeeeee eeee t8qgb t6iaaaaaaaa aaaaaaaa eeeeeeee eeee t8qgb"
  v3$(13)="t3o2i o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr brbr":v3$(13)=v3$(13)+v3$(13)
  v4$(13)="o4t8w.e qo5idp5sri#csr o4ib h.a irb wb we o5h#c o4a wg r"
  v5$(13)="t6o3 ieeeeeeee eeeeeeee o2bbbbbbbb bbbbo3 t8qd#f t6io3eeeeeeee eeeeeeee o2bbbbbbbb bbbbo3 t8qd#f"
  v6$(13)="t3o2i o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr brbr":v6$(13)=v6$(13)+v6$(13)

  ' *** final interlude 2 ***
  v1$(14)="o6t8w.#c q#cio5bo6#c hd iref heir"
  v2$(14)="t7m0o2 sarararararararar arararararararar #ar#ar#ar#ar#ar#ar#ar#ar t8q.eieqee"
  v3$(14)="t3i o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr brr bbrbr"
  v4$(14)="o5t8w.#c q#cio4bo5#c hd iref heir"
  v5$(14)="t7m0p0o3 serererererererer erererererererer frfrfrfrfrfrfrfr t8o2 q.bibqbb"
  v6$(14)="t3i o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr o1gr o2gr o1gg o2gr brr bbrbr"

  ' *** final im alive chorus ***
  v1$(15)="t7m5o5p0 qbb p3o6h.#c o5qbq.bir qp0bb p3h.o6#cp1o5qbq.bir qp0bb p3h.o6#cp1o5qbq.bir qp0bb t1o6hc"
  v2$(15)="so2ao3ao2ao3ao2ao3ao2ao3a o2ao3ao2ao3ao2ao3ao2ao3a o2q.e ieqee":v2$(15)="t7p0m1o2qee"+v2$(15)+v2$(15)+v2$(15)+"p1t1hf"
  v3$(15)="t3i o2brbr o1gr o2gr o1gg o2gr brrb brbr o1gr o2gr o1gg o2gr brrb brbr o1gr o2gr o1gg o2gr brrb brbr o1hg"
  v4$(15)="p0t7o5q#g#g p2h.a q#gq.#gir q#g#g h.a q#gq.#gir q#g#g h.a q#gq.#gir q#g#g t1 p1sfho2f"
  v5$(15)="qt6o4ee o5ip0ae#co4a o5ip0ae#co4a o5b#geo4b o5b#geo4b o5ip0ae#co4a o5ip0ae#co4a o5b#geo4b o5b#geo4b"
  v5$(15)=v5$(15)+"o5sp0ae#ceae#ce ae#ceae#ce b#ge#gb#ge#g b#ge#gb#ge#g o5t1sep1o2he"
  v6$(15)="t3i o2brbr o1gr o2gr o1gg o2gr brrb brbr o1gr o2gr o1gg o2gr brrb brbr o1gr o2gr o1gg o2gr brrb brbr o1hg"
#endif
  return

'---------
.poll_play
'---------
  if rplay(1) then return

  music_chunk_index=map_bar_to_chunk(bar_index)
  
  play v1$(music_chunk_index),v2$(music_chunk_index),v3$(music_chunk_index)
  sleep .02
  play ,,,v4$(music_chunk_index),v5$(music_chunk_index),v6$(music_chunk_index)

  bar_index=bar_index+1
  if bar_index>23 then end
  return
ÿ