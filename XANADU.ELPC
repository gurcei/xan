#output "xanadu-basic65"

'============
.declarations
'============
  ' music related vars
  ' ------------------
  #declare music_chunk_index     ' old t
  #declare bar_index             ' old tt
  #declare map_bar_to_chunk(10)  ' old m()
  #declare v1$(4), v2$(4), v3$(4), v4$(4)

'====
.main
'====
  gosub init
  gosub bas_logo
  ' gosub xanadu_title
  ' gosub petscii_story

  ' todo: load next part of demo (dancing muses)
  end


'========
.bas_logo
'========
  background 1
  border 1
  ^^edma 0, 2000, $40000, $800
  ^^edma 0, 2000, $407d0, $ff80000
  gosub poll_play
  return

'====
.init
'====
  ' switch back to upper-case
  poke $d018, peek($d018) and $fd

  ^^bload "intro.dat",p($40000),r

  ' $4,0000 +------------------+ INTRO.DAT
  '         ! BAS logo chars   !
  ' $4,07D0 +..................+
  '         ! BAS logo colors  !
  ' $4,0FA0 +..................+
  '         ! 32 x RGB PALETTE !
  ' $4,1000 +..................+
  '         ! TITLE colors     !
  ' $4,270C +..................+

  gosub init_music

  return


'==========
.init_music
'==========
  play
  bar_index=0
  map_bar_to_chunk(0)=0
  map_bar_to_chunk(1)=1
  map_bar_to_chunk(2)=1
  map_bar_to_chunk(3)=2
  map_bar_to_chunk(4)=3
  filter 1,100,1
  filter 2,100,1
  envelope 2,3,3,3,10,0
  envelope 4,9,4,9,8
  envelope 7,0,9,9,0,2,1100
  envelope 8,0,12,3,5,2,1100
  envelope 9,0,10,3,5,2,1100

  ' *** voice1 - guitar1 ***
  ' ------------------------
  v1$(0)="t2o6m2 qdir qc.r qdir qc.r qdir qc.r qdir qc.r"
  v1$(1)="t2o6m2 qdir qc.r qdir qc.r p9a ir qair p0idp9o5a o5iao6da q.g ido5a ibo6dg q.a igd o5bo6eg q.a ige o5ibo6dg q.a igd  o5bo6egqa ig#fe "
  v1$(2)="t2o6m2 qdir qc.r qdir qc.r  iddfq.difd o5q.#aho6cir o5#aq#ao6hcir iddfhdir  ddfq.difd o5q.#aho6cir o5#aq#ao6hcir o5q.a o6he ir
  v1$(3)="t2o6m2 igg#ghgir o5q.g o6hd ir aa#aha ir a o6d #f qe i#f qd  q.#f.edo6.g.#fe o5g i#a o6 qe o5i#a o6qe qdir qc.r qdir qc.r
  v1$(3)=v1$(3)+"o6q.g.#fe o5g i#a o6qe o5i#a o6qe qdir qc.r qdir qc.r qdir qc.r qdir qc.r"

  ' *** voice2 - guitar2 ***
  ' ------------------------
  v2$(0)="t2o5m4 qair q#g.r qair q#g.r qair q#g.r qair q#g.r"
  v2$(1)="t2o5m4 qair q#g.r qair q#g.r  o5 q.#c .d o6i#fd o5d#fao6q.d io5a#f  egbo6q.e o5ibg eao6#cq.e i#co5a egbo6q.e o5ibg eao6#cqe id#cd"
  v2$(2)="t2o5m4 qair q#g.r qair q#g.r  iaao6co5q.aiga q.fhg ir ifqf hg ir iaao6co5ha ir  aao6co5q.aiga q.fhg irfqf hg ir q.e .a o6e "
  v2$(3)="t2o5m4 igg#g hg ir q.d hg ir ddghd ir ddaqgiaqd q.a.g#f .#a .a g  o5qeigq#aig#ao6c qair q#g.r qair q#g.r q.#a .a g o5qeigq#aig#ao6c"
  v2$(3)=v2$(3)+"qair q#g.r qair q#g.r qair q#g.r qair q#g.r"

  ' *** voice3 - bass ***
  ' ---------------------
  v3$(0)="x1t7o2m1 id qr p1 icd qr icd sf#f qd ir o1ao2co1a   o2id qr p1 icd qr icd sf#f qd ir o1ao2co1a"
  v3$(1)="x1t7o2m1 id qr p1 icd qr icd sf#f qd qr ir o1a t8 o2q.#cqd ide#f q.#f o1a ia o2q#c o1q.e e iebg q.a a ia o2qc q.e e ie o1 qb q.a a o2ie qa"
  v3$(2)="x1t7o2m1 id qr p1 icd qr icd sf#f qd ir o1ao2co1a"
  v3$(2)=v3$(2)+"t9o2idddddddd o1#a#a#a#a#a#a#a#a #a#a#a#a#a#a#a#a o2ddfqdiddddddddddd o1#a#a#a#a#a#a#a#a #a#a#a t8o2qciccc o1q.aaiaaa"
  v3$(3)="o2q.ddiddd o1q.ggi#aa#a q.#a#ai#aq#a q.aaiaqa o2q.ddio1go2qd q.eeieqe o1q.aaiaqa t7o2 id qr p1 icd qr icd sf#f qd ir o1ao2co1a"
  v3$(3)=v3$(3)+"o2q.eeieqe o1q.aaiaqa t7o2 id qr p1 icd qr icd sf#f qd ir o1ao2co1a  t7o2 id qr p1 icd qr icd sf#f qd ir o1ao2co1a"

  ' *** voice4 - melody ***
  ' -----------------------
  v4$(0)=""
  v4$(1)="t4o5p0 qr m2id m4qd p5 m1o4#g h.a iaqa   o5q.e h#f ir #f#fa qg ig#f w m5 .g p9m2o4ia o5i.b.a sg#f agabab qg o4iab o5h#f sg#f q.e.d"
  v4$(2)="o5t4p7wdhrq.ricp5o5iddfq.gifdq.fsedhciffaqgifdscp2h.dp5i.ricddfq.gifd q.f sed hc iffaqgifd igp1h.a p7ira"
  v4$(3)="t4 io6dd#dqdico5#ah.#aq.r iaa#aqaiagp1wap7ir"
  v4$(3)=v4$(3)+"hri#fgaq.gi#fg#fherir dhdrr ir#fgaq.gi#fg#fherir dhd"
  return

'---------
.poll_play
'---------
  if rplay(1) then return

  music_chunk_index=map_bar_to_chunk(bar_index)
  
  play v1$(music_chunk_index),v3$(music_chunk_index),v4$(music_chunk_index)
  sleep .05
  play ,,,v2$(music_chunk_index),v3$(music_chunk_index),v4$(music_chunk_index)

  bar_index=bar_index+1:if bar_index=5 then bar_index=1
  return
ÿ